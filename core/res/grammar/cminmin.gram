(* the first variable is also the start variable *)
<program> ::= <function> | <function> <program>
<function> ::= "fn" "identifier" "(" <arguments> ")" "{" <body> "}" | "fn" "identifier" "(" ")" "{" <body> "}" | "fn" "identifier" "(" ")" "{" "}"
<arguments> ::= "identifier" | "identifier" "," <arguments>
<body> ::= <statement> | <statement> <body>
<statement> ::= <assignment> | <function_call> ";" | <if> | <return> | <while>
<assignment> ::= "let" "identifier" "=" <expression> ";" | "identifier" "=" <expression> ";"
<call_arguments> ::= <expression> | <expression> "," <call_arguments>
<if> ::= "if" <expression> "{" <body> "}" | "if" <expression> "{" <body> "}" "else" "{" <body> "}"
<return> ::= "return" <expression> ";"
<while> ::= "while" <expression> "{" <body> "}"
<expression> ::= <term> | <expression> "+" <term> | <expression> "-" <term> | <expression> "=" "=" <term> | <expression> "!" "=" <term> | <expression> "<" <term> | <expression> ">" <term> | <expression> "<" "=" <term> | <expression> ">" "=" <term>
<term> ::= <factor> | <term> "*" <factor> | <term> "/" <factor>
<factor> ::= "literal" | "identifier" | "(" <expression> ")" | "!" "(" <expression> ")" | "-" <factor> | <function_call>
<function_call> ::= "identifier" "(" ")" | "identifier" "(" <call_arguments> ")"
